#include <WiFi.h>
#include <PubSubClient.h>

// Pinos dos LEDs
#define LED_VERDE 18
#define LED_AMARELO 19
#define LED_VERMELHO 21

// --- CONFIGURAÇÕES ---
const char* ssid = "Wokwi-GUEST";
const char* password = "";
const char* mqtt_server = "broker.hivemq.com";

// Tópico onde ele escuta as ordens
const char* topic_subscribe = "sp/comando/esp32"; 

WiFiClient espClient;
PubSubClient client(espClient);

void callback(char* topic, byte* payload, unsigned int length) {
  String mensagem = "";
  for (int i = 0; i < length; i++) {
    mensagem += (char)payload[i];
  }

  // Apaga tudo antes de acender o certo
  digitalWrite(LED_VERDE, LOW);
  digitalWrite(LED_AMARELO, LOW);
  digitalWrite(LED_VERMELHO, LOW);

  // Compara o texto recebido
  if (mensagem == "Bom") digitalWrite(LED_VERDE, HIGH);
  else if (mensagem == "Moderado" || mensagem == "ALERTA") digitalWrite(LED_AMARELO, HIGH);
  else if (mensagem == "Ruim" || mensagem == "PERIGO") digitalWrite(LED_VERMELHO, HIGH);
}

void reconnect() {
  while (!client.connected()) {
    Serial.print("Tentando conectar no MQTT...");
    String clientId = "ESP32_ATUADOR_LEDS_FIXO"; 
    
    if (client.connect(clientId.c_str())) {
      Serial.println("Conectado!");
      client.subscribe(topic_subscribe); // Importante: Se inscreve para ouvir
    } else {
      Serial.print("falhou, erro rc=");
      Serial.print(client.state());
      Serial.println(" tentando de novo em 5s");
      delay(5000);
    }
  }
}

void setup() {
  Serial.begin(115200);
  pinMode(LED_VERDE, OUTPUT);
  pinMode(LED_AMARELO, OUTPUT);
  pinMode(LED_VERMELHO, OUTPUT);
  
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) delay(500);
  
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
}

void loop() {
  if (!client.connected()) reconnect();
  client.loop();
}
